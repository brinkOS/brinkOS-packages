# Maintainer: James Brink <brink.james@gmail.com>
# This Package was forked from the ArchLinux User Repository
# All credit to the AUR maintainers below.
# -----------------------------------------------------------------
# Original Maintainer: Conor Anderson <conor@conr.ca>
pkgname=brinkOS-wire
pkgver=3.0.2816
pkgrel=1
pkgdesc='Modern, private messenger. Based on Electron.'
arch=('x86_64' 'i686')
url='https://wire.com/'
license=('GPL3')
conflicts=('wire-desktop-bin' 'wire-desktop')
depends=('alsa-lib' 'gconf' 'gtk2' 'libxss' 'libxtst' 'nss' 'xdg-utils')
makedepends=('cargo' 'npm' 'python2' 'git')
optdepends=('hunspell-en: for English spellcheck support'
            'noto-fonts-emoji: for colorful emoji made by Google'
            'ttf-emojione: for colorful emoji made by EmojiOne')
provides=('wire-desktop')
source=("${pkgver}.tar.gz::https://github.com/wireapp/wire-desktop/archive/release/"$pkgver".tar.gz"
        "wire-desktop.desktop")
sha256sums=('7c9ec60dfbb1aedd60eeb23d248e3d539301c94ff1f44b58a66737324a0780c3'
            'cc9056cecff2aa49a9ce9c8376d57ec8c7c2cb8174f7966b5cdccbeb2e3751ea')

build() {
  cd "${srcdir}/wire-desktop-release-${pkgver}"
  npm install
  $(npm bin)/grunt 'linux-other'
}

package() {
  # Place files
  install -d "${pkgdir}/usr/lib/wire-desktop"
  cp -a "${srcdir}/wire-desktop-release-${pkgver}"/wrap/dist/linux*unpacked/* "${pkgdir}/usr/lib/wire-desktop"

  # Symlink main binary
  install -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/wire-desktop/wire-desktop" "${pkgdir}/usr/bin/wire-desktop"

  # Place desktop entry and icon
  desktop-file-install -m 644 --dir "${pkgdir}/usr/share/applications/" "${srcdir}/wire-desktop.desktop"
  for res in 32x32 256x256; do
    install -dm755 "${pkgdir}/usr/share/icons/hicolor/${res}/apps"
    install -Dm644 "${srcdir}/wire-desktop-release-${pkgver}/resources/icons/${res}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${res}/apps/wire-desktop.png"
  done

  # Spellcheck dictionaries
  rm -rf "${pkgdir}/usr/lib/wire-desktop/resources/app/node_modules/spellchecker/vendor/hunspell_dictionaries"
  ln -s "/usr/share/hunspell" "${pkgdir}/usr/lib/wire-desktop/resources/app/node_modules/spellchecker/vendor/hunspell_dictionaries"

  # Place license files
  for license in "LICENSE.electron.txt" "LICENSES.chromium.html"; do
    install -Dm644 "${pkgdir}/usr/lib/wire-desktop/${license}" "${pkgdir}/usr/share/licenses/wire-desktop/${license}"
    rm "${pkgdir}/usr/lib/wire-desktop/${license}"
  done
}
