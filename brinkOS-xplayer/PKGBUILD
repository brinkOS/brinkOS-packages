# Maintainer: James Brink <brink.james@gmail.com>
# This package was forked from AUR
# Original Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=brinkOS-xplayer
pkgver=1.6.1
pkgrel=2
pkgdesc="Simple media player. X-Apps Project."
arch=('i686' 'x86_64' 'armv7h')
license=('GPL')
depends=('clutter-gtk' 'clutter-gst' 'xplayer-plparser'
    'desktop-file-utils' 'iso-codes' 'yelp-xsl'
    'libpeas' 'gsettings-desktop-schemas' 'dbus-glib'
    'python2-gobject' 'gst-plugins-base' 'gst-plugins-good'
    'xapps')
makedepends=('gnome-common' 'gtk-doc' 'gobject-introspection' 'vala')
optdepends=('gst-libav: Extra media codec support')
provides=(xplayer)
conflicts=('xplayer-git' 'xplayer')
url='https://github.com/linuxmint/xplayer'
install=brinkOS-xplayer.install

source=("xplayer-${pkgver}.tar.gz::https://github.com/linuxmint/xplayer/archive/${pkgver}.tar.gz")
md5sums=('6c1518553be0a9098768dd8b2f58fd91')

build() {
    cd ${srcdir}/xplayer-${pkgver}

    # https://github.com/linuxmint/xplayer/issues/22
    # No switch to disable grilo, so just bump the required
    # version to something that obviously won't be satisfied.
    sed -i 's/GRILO_REQS=0.2.0/GRILO_REQS=0.9.0/g' configure.ac

    ./autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/xplayer"
    make
}

package(){
    cd ${srcdir}/xplayer-${pkgver}
    make DESTDIR="$pkgdir/" install
}
