# Maintainer: James Brink <brink.james@gmail.com>
# This Package was forked from the ArchLinux User Repository
# All credit to the AUR maintainers below.
# -----------------------------------------------------------------
# Original Maintainer: Thomas Bork <tab.epic@gmail.com>

pkgname=brinkOS-pamac
pkgver=6.6.4
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm - classic version"
arch=('x86_64')
url="https://github.com/cromnix/pamac-classic"
license=('GPL3')
depends=('curl' 'libsoup' 'polkit' 'pacman' 'libnotify' 'vte3')
makedepends=('vala' 'cmake' 'gettext')
optdepends=('polkit-gnome: needed for authentication in Cinnamon, Gnome'
            'mate-polkit: needed for authentication in MATE'
            'lxsession: needed for authentication in Xfce, LXDE etc.')
conflicts=('pamac' 'pamac-aur')
provides=('pamac' 'pamac-aur')
install=brinkOS-pamac.install

source=("pamac-classic-$pkgver-$pkgrel.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('258a6787ce98ebffc17b1d4ba02b1c70927997ce61b0b4ea168fa00036d910b6')

#prepare() {
#  cd "${srcdir}/pamac-classic-$pkgver"
#  # patches here
#}

build() {
  cd "${srcdir}/pamac-classic-$pkgver"

  # configure with AUR - add --disable-aur to disable AUR support
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --disable-icon-update

  # build
  make
}

package() {
  cd "${srcdir}/pamac-classic-$pkgver"
  make DESTDIR="$pkgdir" install
}
