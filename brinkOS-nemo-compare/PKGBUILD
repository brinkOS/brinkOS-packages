# Maintainer: James Brink <brink.james@gmail.com>
# Forked from AUR
# Original AUR Maintainer: Sebastian Lau <lauseb644 _at_ gmail _dot_ com>

pkgname=brinkOS-nemo-compare
pkgver=3.6.0
pkgrel=1
pkgdesc="Context menu comparison extension for Nemo file manager"
arch=("any")
url="https://github.com/linuxmint/nemo-extensions"
license=('GPL3')
depends=('nemo>=3.2' 'nemo-python>=1.0' 'python2-xdg' 'pygtk')
groups=('nemo-extensions')
conflicts=('nemo-compare')
optdepends=('diffuse: Additional comparison options (preferred diff, three-way, multi-compare)'
	   'kompare: Additional comparison options (preferred diff, three-way, multi-compare)'
	   'fldiff: Additional comparison options (preferred diff, three-way, multi-compare)')
install=brinkOS-nemo-compare.install
source=("nemo-extensions-$pkgver.tar.gz::https://github.com/linuxmint/nemo-extensions/archive/$pkgver.tar.gz")
sha256sums=('593aaf206ef50a8185e1f71c9018ce6a8b4bcb82b4684494e069f9045ba11fd1')


package() {
  cd "${srcdir}/nemo-extensions-${pkgver}/nemo-compare"

  # Python2 fix
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  install -D data/nemo-compare-notification -t "${pkgdir}/usr/share/nemo-compare"
  install -D src/* -t "${pkgdir}/usr/share/nemo-compare/"

  install -d "${pkgdir}/usr/share/nemo-python/extensions"
  install -d "${pkgdir}/usr/bin"

  ln -s /usr/share/nemo-compare/nemo-compare.py "${pkgdir}/usr/share/nemo-python/extensions/nemo-compare.py"
  ln -s /usr/share/nemo-compare/nemo-compare-preferences.py "${pkgdir}/usr/bin/nemo-compare-preferences"
}
