# Maintainer: James Brink <brink.james@gmail.com>
# Forked from AUR
# Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=brinkOS-pix
pkgver=1.6.2
pkgrel=2
pkgdesc="Image viewer and browser based on gthumb. X-Apps Project."
arch=('i686' 'x86_64' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'gconf' 'librsvg' 'clutter-gtk'
        'gst-plugins-base-libs' 'gsettings-desktop-schemas' 'libwebp' 'webkit2gtk')
makedepends=('gnome-common' 'intltool' 'itstool'
        'liboauth' 'libchamplain' 'libopenraw' 'exiv2')
optdepends=('gstreamer: Video support'
    'exiv2: Embedded metadata support'
    'libjpeg-turbo: Jpeg writing support'
    'libtiff: Tiff writing support'
    'dcraw: Support for RAW photos'
    'brasero: Burn discs'
    'liboauth: Web albums'
    'libchamplain: Map Viewer')
provides=(pix)
conflicts=('pix-git' 'pix')
url='https://github.com/linuxmint/pix'
install=brinkOS-pix.install

source=("pix-${pkgver}.tar.gz::https://github.com/linuxmint/pix/archive/${pkgver}.tar.gz")
md5sums=('a5f952d65bd47615d5a50067bc238ef6')


build() {
    cd ${srcdir}/pix-${pkgver}
    gnome-autogen.sh --prefix="/usr" \
        --localstatedir="/var" \
        --libexecdir="/usr/lib/pix" \
        -disable-static

    # Copy some files that ended up in the wrong directory.
    # This doesn't happen on mint. See this github issue:
    # https://github.com/linuxmint/pix/issues/7
    if [ -d 'tests/$(top_srcdir)/' ]
    then
        cp 'tests/$(top_srcdir)/pix/.deps/dom_test-dom.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/glib_utils_test-glib-utils.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/gsignature_test-gsignature.Po' "pix/.deps/"
        cp 'tests/$(top_srcdir)/pix/.deps/oauth_test-gsignature.Po' "pix/.deps/"
    fi

    make
}

package(){
    cd ${srcdir}/pix-${pkgver}
    make DESTDIR="$pkgdir/" install
}
