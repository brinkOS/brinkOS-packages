# Maintainer: James Brink <brink.james@gmail.com>
# Forked from AUR
# Contributor:  danyf90 <daniele.formichelli@gmail.com>
# Contributor: Philipp 'TamCore' B. <philipp [at] tamcore [dot] eu>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Christoph Brill <egore911-at-gmail-dot-com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>
# Original Maintainer: Tad Fisher <tadfisher at gmail dot com>
# Original Maintainer: Philippe HÃ¼rlimann <p@hurlimann.org>

pkgname=brinkOS-android-studio
pkgver=3.1.2.0
pkgrel=1
_build=173.4720617
pkgdesc="The official Android IDE (Stable branch)"
arch=('i686' 'x86_64')
url="http://tools.android.com/"
license=('APACHE')
makedepends=('unzip')
depends=('alsa-lib' 'freetype2' 'libxrender' 'libxtst')
optdepends=('gtk2: GTK+ look and feel'
            'libgl: emulator support')
options=('!strip')
source=("https://dl.google.com/dl/android/studio/ide-zips/$pkgver/android-studio-ide-$_build-linux.zip"
        "android-studio.desktop")
sha256sums=('13f290279790df570bb6592f72a979a495f7591960a378abea7876ece7252ec1'
            '73cd2dde1d0f99aaba5baad1e2b91c834edd5db3c817f6fb78868d102360d3c4')

if [ "$CARCH" = "i686" ]; then
    depends+=('java-environment')
fi

package() {
  cd $srcdir/android-studio

  # Install the application
  install -d $pkgdir/{opt/android-studio,usr/bin}
  cp -a bin gradle lib jre plugins $pkgdir/opt/android-studio
  ln -s /opt/android-studio/bin/studio.sh $pkgdir/usr/bin/android-studio

  # Add the icon and desktop file
  install -Dm644 bin/studio.png $pkgdir/usr/share/pixmaps/android-studio.png
  install -Dm644 $srcdir/android-studio.desktop $pkgdir/usr/share/applications/android-studio.desktop

  chmod -R ugo+rX $pkgdir/opt
}
