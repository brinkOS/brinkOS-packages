# Maintainer: James Brink <brink.james@gmail.com>
# Forked from AUR
# Original Maintainer: Nate Simon <aurpkg (at natesimon.net)>

pkgname=brinkOS-xed
pkgver=1.6.3
pkgrel=1
pkgdesc="A small and lightweight text editor. X-Apps Project."
arch=('i686' 'x86_64' 'armv7h')
license=('GPL')
depends=('gtksourceview3' 'enchant' 'desktop-file-utils' 'libsm'
        'libpeas')
makedepends=('gnome-common' 'iso-codes' 'gobject-introspection')
provides=(xed)
conflicts=('xed-git' 'xed')
url='https://github.com/linuxmint/xed'
source=("xed-${pkgver}.tar.gz::https://github.com/linuxmint/xed/archive/${pkgver}.tar.gz")
md5sums=('b7aa267ecde3de3753c38e0092811e8b')


prepare() {
    cd ${srcdir}/xed-${pkgver}
    # https://www.archlinux.org/todo/enchant-221-rebuild/
    sed -i 's/, enchant/, enchant-2/' configure.ac
}

build() {
    cd ${srcdir}/xed-${pkgver}
    ./autogen.sh --with-gtk=3.0 --prefix="/usr" \
         --localstatedir="/var" \
         --libexecdir="/usr/lib/xed"
    make CFLAGS='-w -O2'
}

package(){
    cd ${srcdir}/xed-${pkgver}
    make DESTDIR="$pkgdir/" install
}
